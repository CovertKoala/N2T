// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //Register input and selection
    Inc16(in=REGO, out=ADD1);
    Mux16(sel=inc, a=REGO, b=ADD1, out=MI);
    Mux16(sel=load, a=MI, b=in, out=ML);
    Mux16(sel=reset, a=ML, b=false, out=MRIN);

    //Register load input - load = 1 for Load, Inc, and Reset.
    Or(a=reset,b=inc,out=OR1);
    Or(a=OR1,b=load,out=RLOAD);

    //Register
    Register(in=MRIN, load=RLOAD, out=REGO, out=out);
}
